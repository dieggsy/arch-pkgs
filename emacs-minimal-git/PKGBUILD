# -*- mode: sh -*-
# Maintainer: dieggsy <dieggsy@pm.me>

pkgname=emacs-minimal-git
pkgver=27.0.50.3907.g9530108fbc
pkgrel=1
pkgdesc="The extensible, customizable, self-documenting real-time display editor"
url="http://www.gnu.org/software/emacs/emacs.html"
arch=('x86_64')
license=('GPL3')
depends=('libmagick' 'libxml2' 'jansson' 'gnutls' 'zlib' 'gmp')
makedepends=('git')
provides=(emacs emacs-git)
conflicts=(emacs emacs-git)
source=(git://git.sv.gnu.org/emacs.git)
md5sums=('SKIP')

pkgver() {
    cd emacs
    printf "%s.%s" \
           "$(grep AC_INIT configure.ac | cut -d,  -f2 | tr -d '[:space:]')" \
           "$(git describe --tags --long | cut -d- -f3- | sed 's/-/./g')"
}

prepare() {
    cd emacs
    # to use a custom config.h, place it in the package directory
    [[ -x configure ]] || (./autogen.sh)
}

build(){
    cd emacs
    ./configure --prefix=/usr --sysconfdir=/etc --libexecdir=/usr/lib \
                --localstatedir=/var --mandir=/usr/share/man --without-all \
                --with-xml2 --with-json --with-dbus --with-gnutls --with-zlib \
                --with-modules --with-threads --with-compress-install \
                --with-libgmp
    make
}

package() {
    cd emacs
    make DESTDIR="$pkgdir" install

    # # fix user/root permissions on usr/share files
    find "$pkgdir"/usr/share/emacs/ -exec chown root:root {} +

}
